// part of Polish Road Set 2
// Graphic by Sojita, McZapkie
// NML code by McZapkie

//Mail van/Minibus switches
//mail
switch (FEAT_ROADVEHS, SELF, switch_MVMB_refit_from_mailvan, (
hasbit((extra_callback_info1 & 0xFFFF0000) >> 16, CC_MAIL)) ) {
   1 : return 0;
   return CB_FAILED;
}
//valuables
switch (FEAT_ROADVEHS, SELF, switch_MVMB_refit_from_armouredvan, (
hasbit((extra_callback_info1 & 0xFFFF0000) >> 16, CC_ARMOURED )) ) {
   1 : return 0;
   return CB_FAILED;
}
//passengers
switch (FEAT_ROADVEHS, SELF, switch_MVMB_refit_from_minibus, (
hasbit((extra_callback_info1 & 0xFFFF0000) >> 16, CC_PASSENGERS )) ) {
   1 : return 0;
   return CB_FAILED;
}
//express goods
switch (FEAT_ROADVEHS, SELF, switch_MVMB_refit_from_goodsvan, (
hasbit((extra_callback_info1 & 0xFFFF0000) >> 16, CC_EXPRESS ) && 
!hasbit((extra_callback_info1 & 0xFFFF0000) >> 16, CC_PASSENGERS )
) ) {
   1 : return 0;
   return CB_FAILED;
}
switch (FEAT_ROADVEHS, SELF, switch_MVMB_refit_cost, cargo_classes ) {
   bitmask(CC_MAIL) :  return switch_MVMB_refit_from_mailvan;
   bitmask(CC_ARMOURED) :  return switch_MVMB_refit_from_armouredvan;
   bitmask(CC_PASSENGERS) : return switch_MVMB_refit_from_minibus;
   return switch_MVMB_refit_from_goodsvan;
}

switch(FEAT_ROADVEHS, SELF, check_pass_subtype_MVMB_text_switch, hasbit(cargo_classes, CC_PASSENGERS) ) {  
	1: return switch_livery_subtype_text;  // string(STR_MINIBUS_SUBTYPE);  
	return string(STR_GOODVAN_SUBTYPE);  
} 
switch(FEAT_ROADVEHS, SELF, subtype_MVMB_text_switch, cargo_classes) {  
	bitmask(CC_MAIL): 	return string(STR_MAIL_SUBTYPE);  
	bitmask(CC_ARMOURED): 	return string(STR_ARMOURED_SUBTYPE);   
	return check_pass_subtype_MVMB_text_switch;  
}  

// Mail Van/Minibus definition:

#define MIXED_MVMB(MVMB_Name, STR_V_NAME, STR_VE_DESC, idate, odate, retire, v_life, v_reliab, v_cost, v_fuelsystem, visual_effect_flag, e_displacement, v_vmax, v_power, v_airdrag, v_mass, v_maxloadmass, v_CAP, v_SIT) \
\
switch (FEAT_ROADVEHS, SELF, switch_##MVMB_Name##_text, param_load_multiplier) {  \
	1 : return string(STR_MVMB_DESC, v_maxloadmass*2, v_CAP, string(STR_VE_DESC), e_displacement, string(STR_VARIABLE_COSTS));  \
	2 : return string(STR_MVMB_DESC, v_maxloadmass*4, v_CAP, string(STR_VE_DESC), e_displacement, string(STR_VARIABLE_COSTS));  \
	3 : return string(STR_MVMB_DESC, v_maxloadmass*6, v_CAP, string(STR_VE_DESC), e_displacement, string(STR_VARIABLE_COSTS));  \
  return CB_FAILED;  \
}  \
switch(FEAT_ROADVEHS, SELF, check_pass_##MVMB_Name##_graphic_switch, hasbit(cargo_classes, CC_PASSENGERS) ) {  \
	1: return pass_spriteset_##MVMB_Name##_graphic; \
	return good_spriteset_##MVMB_Name##_graphic;  \
} \
switch(FEAT_ROADVEHS, SELF, spriteset_##MVMB_Name##_graphic_switch, cargo_classes) {  \
	bitmask(CC_MAIL): 	return mail_spriteset_##MVMB_Name##_graphic;  \
	bitmask(CC_ARMOURED): 	return valu_spriteset_##MVMB_Name##_graphic;  \
	return check_pass_##MVMB_Name##_graphic_switch;   \
}  \
switch(FEAT_ROADVEHS, SELF, maxload_##MVMB_Name##_switch, cargo_type_in_veh) {  \
	MAIL: return v_maxloadmass*param_load_multiplier*8/4;  \
	GOOD: return v_maxloadmass*param_load_multiplier*8/8;  \
	ENSP: return v_maxloadmass*param_load_multiplier*8/10;  \
	FMSP: return v_maxloadmass*param_load_multiplier*8/9;  \
	MNSP: return v_maxloadmass*param_load_multiplier*8/5;  \
	VALU: return v_maxloadmass*param_load_multiplier*8/2;  \
	GOLD: return v_maxloadmass*param_load_multiplier*8/8;  \
	DIAM: return v_maxloadmass*param_load_multiplier*8/2;  \
	PASS: return v_CAP;  \
	TOUR: return v_SIT;  \
        return v_maxloadmass*param_load_multiplier*8/cargo_unit_weight;    \
}  \
switch(FEAT_ROADVEHS, SELF, switch_##MVMB_Name##_running_cost, current_speed) { \
	0: return min(!vehicle_is_in_depot * STAFF_RUNNING_COST +  max(0,((v_mass + v_maxloadmass) * (e_displacement / 10) * v_fuelsystem) >> 13), 255); \
	return min(STAFF_RUNNING_COST * max(1, (age_in_days - max_age_in_days - 365 * retire)/365) + max(0,((v_mass + v_maxloadmass) * ((e_displacement / 10) + (current_speed) * (current_speed) * 3) * v_fuelsystem) >> 13), 255); \
} \
if (param_allow_van) { \
 item(FEAT_ROADVEHS, MVMB_Name) { \
	property { \
	name:			   	string(STR_V_NAME, string(STR_MVMB_SUFFIX)); \
	climates_available:	 	POLROAD_CLIMATES; \
	introduction_date:		date(idate, 1, 1);	    \
	model_life:     ((param_keep_old_vehs == 1) || (odate == 0)) ? VEHICLE_NEVER_EXPIRES : (param_keep_old_vehs == 2) ? odate-idate+retire+4+v_life : odate-idate+retire+4; \
	vehicle_life:		   	v_life; \
	retire_early:			retire; \
	reliability_decay:	    	v_reliab; \
	loading_speed:		  	5; \
	cost_factor:		    	v_cost;      \
	running_cost_factor:	min(STAFF_RUNNING_COST + max(0,((v_mass + v_maxloadmass) * ((e_displacement / 10) + (v_vmax) * (v_vmax)) * v_fuelsystem) >> 15), 255); \
	sprite_id:		      	SPRITE_ID_NEW_ROADVEH; \
	speed:			  	v_vmax km/h; \
	refittable_cargo_classes:       bitmask(CC_MAIL, CC_ARMOURED, CC_PASSENGERS, CC_EXPRESS); \
	misc_flags:		   	bitmask(ROADVEH_FLAG_2CC); \
	refit_cost:		   	MVMB_REFIT_COST; \
	cargo_age_period:	 	STANDARD_COMFORT; \
	power:			  	v_power kW; \
	running_cost_base:	   	RUNNING_COST_ROADVEH; \
	cargo_capacity:		 	v_maxloadmass; \
	default_cargo_type:		PASS; \
	weight:			 	v_mass; \
	tractive_effort_coefficient:    0.5; \
	air_drag_coefficient:	   	v_airdrag; \
	visual_effect:      	  	visual_effect(visual_effect_flag, 3); \
	sound_effect: SOUND_CAR_HORN_2; \
	length:      	    		3; \
    } \
\
    graphics { \
	default:	    	spriteset_##MVMB_Name##_graphic_switch;  \
	purchase:	   	spriteset_##MVMB_Name##_purchase;  \
	refit_cost:		switch_MVMB_refit_cost;  \
	cargo_capacity:		maxload_##MVMB_Name##_switch; \
	additional_text:        switch_##MVMB_Name##_text; \
	colour_mapping:         switch_livery_subtype_mapping_WPK; \
	cargo_subtype_text:	subtype_MVMB_text_switch; \
	purchase_running_cost_factor: return min(STAFF_RUNNING_COST + max(0,((v_mass + v_maxloadmass) * (e_displacement / 10 + (v_vmax) * (v_vmax)) * v_fuelsystem) >> 15), 255); \
	running_cost_factor:	switch_##MVMB_Name##_running_cost; \
    }  \
 }  \
}

#include "../src/gfx/other/nysan59.pnml"
// MIXED_MVMB(MVMB_Name, STR_V_NAME, idate, odate, retire, v_life, v_reliab, v_cost, v_fuelsystem, e_displacement, v_vmax, v_power, v_airdrag, v_mass, v_maxloadmass, v_CAP, v_SIT)
MIXED_MVMB(NysaN59, STR_NYSAN59_NAME, STR_NYSAN59_DESC, 1957, 1972, 0, 15, 20, 10, PETROL_ENGINE, VISUAL_EFFECT_DIESEL, 2120, 85, 37, 0.12, 1, 2, 9, 9)

#include "../src/gfx/other/nysa522.pnml"
MIXED_MVMB(Nysa522, STR_NYSA522_NAME, STR_NYSA522_DESC, 1968, 1994, 10, 20, 5, 11, PETROL_ENGINE, VISUAL_EFFECT_DIESEL, 2120, 90, 51, 0.12, 1, 2, 10, 10)

#include "../src/gfx/other/mercedes_vario.pnml"
MIXED_MVMB(Mercedes_Vario, STR_MERCEDES_VARIO_NAME, STR_MERCEDES_VARIO_DESC, 1989, 2013, 25, 14, 1, 14, DIESEL_ENGINE_CR, VISUAL_EFFECT_DISABLE, 2874, 120, 95, 0.05, 2, 2, 14, 10)

#include "../src/gfx/other/mercedes_sprinter.pnml"
MIXED_MVMB(Mercedes_Sprinter, STR_MERCEDES_SPRINTER_NAME, STR_MERCEDES_SPRINTER_DESC, 1995, 0, 0, 12, 1, 15, DIESEL_ENGINE_CR, VISUAL_EFFECT_DISABLE, 2148, 140, 110, 0.04, 2, 3, 16, 12)


// part of Polish Road Set
// Graphic by McZapkie
// NML code by McZapkie

// two horse heavy omnibus:


/* */
#define twohorse_omnibus_mass 3
#define twohorse_omnibus_speed 10
#define twohorse_omnibus_sitpass 12
#define twohorse_omnibus_stdpass 16
#define twohorse_omnibus_totpass (twohorse_omnibus_stdpass + twohorse_omnibus_sitpass)


spriteset(spriteset_twohorse_omnibus_purchase, "src/gfx/bus/horse_omnibus.png") { template_sprite_window_sideview8(40) }

spriteset(horse_omnibus_graphic, "src/gfx/bus/horse_omnibus.png") { template_sprite6(8) }


switch (FEAT_ROADVEHS, SELF, consist_twohorse_omnibus_graphic_switch, position_in_consist) { 
    0 : return switch_randomize1_first_horsepair_graphics; 
    1 : return horse_omnibus_graphic; 
return CB_FAILED;
}
 
switch (FEAT_ROADVEHS, SELF, switch_twohorse_omnibus_articulated, extra_callback_info1) { 
    1 : return twohorse_omnibus; 
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; 
} 
switch(FEAT_ROADVEHS, SELF, check_twohorse_omnibus_length_switch, position_in_consist) {  
	0 : return 4; 
	1 : return 5;  
}    
switch(FEAT_ROADVEHS, SELF, switch_twohorse_omnibus_capacity, position_in_consist) {
	0: return 0;
	1: return twohorse_omnibus_totpass;
	return CB_FAILED;
}

if (param_allow_slow_vehs) {
 item(FEAT_ROADVEHS, twohorse_omnibus) { 
	property { 
	name:			   	string(STR_2HORSE_OMNIBUS_NAME); 
	climates_available:	 	POLROAD_CLIMATES; 
	introduction_date:		date(1822, 1, 1);	    
	model_life:     (param_keep_old_vehs == 1) ? VEHICLE_NEVER_EXPIRES : ((param_keep_old_vehs == 2) ? 123+10+20 : 123+10);  
	vehicle_life:		   	20; 
	retire_early:			10; 
	reliability_decay:	    	5; 
	loading_speed:		  	GET_LOADING_SPEED(1, 0);  
	cost_factor:		    	12;      
	running_cost_factor:	min(4 * STAFF_RUNNING_COST /10, 255); 
	sprite_id:		      	SPRITE_ID_NEW_ROADVEH; 
	speed:			  	twohorse_omnibus_speed km/h;  
	refittable_cargo_classes:      	bitmask(CC_PASSENGERS); 
	misc_flags:		   	bitmask(ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_2CC, ROADVEH_FLAG_NO_BREAKDOWN_SMOKE); 
	refit_cost:		   	0; 
	cargo_age_period:	 	GET_COMFORT(COMFORT_HARD, 0, twohorse_omnibus_sitpass, twohorse_omnibus_totpass); 
	power:			  	20 hp; 
	running_cost_base:	   	RUNNING_COST_ELECTRIC; 
	cargo_capacity:		 	twohorse_omnibus_totpass; 
	default_cargo_type:		PASS; 
	weight:			 	twohorse_omnibus_mass; 
	tractive_effort_coefficient:    1; 
	air_drag_coefficient:	   	0.8; 
	visual_effect:      	  	visual_effect(VISUAL_EFFECT_DISABLE, 0); 
//	road_type:			OFFR;
	sound_effect: SOUND_HORSE;
	length:      	    		4; 
    } 
    graphics { 
	default:	    	consist_twohorse_omnibus_graphic_switch;  
	purchase:	   	spriteset_twohorse_omnibus_purchase;  
	articulated_part:	switch_twohorse_omnibus_articulated; 
	cargo_capacity:		switch_twohorse_omnibus_capacity; 
	purchase_cargo_capacity: twohorse_omnibus_totpass/2;
	length:			check_twohorse_omnibus_length_switch;
	additional_text:        string(STR_2HORSE_OMNIBUS_DESC, twohorse_omnibus_sitpass, twohorse_omnibus_stdpass, GET_COMFORT(COMFORT_HARD, 0, twohorse_omnibus_sitpass, twohorse_omnibus_totpass), GET_LOADING_SPEED(1, 0)); 
	cargo_subtype_text: 	switch_livery_subtype_text;
	colour_mapping:         switch_livery_subtype_mapping;      
    }  
 }
}


if ((param_allow_slow_vehs) && OFFR_EXISTS)  {
 item(FEAT_ROADVEHS, twohorse_omnibus) { 
	property { 
	road_type:			OFFR;
    } 
 }
}

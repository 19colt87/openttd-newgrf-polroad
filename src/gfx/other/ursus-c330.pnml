// part of Polish Road Set
// Graphic by McZapkie
// NML code by McZapkie


// Ursus C330 farm tractor:


#define UrsusC330_mass 3
#define UrsusC330_vmax 26
#define UrsusC330_edisp 3120


spriteset(spriteset_UrsusC330_purchase, "src/gfx/other/UrsusC330.png") { template_sprite_window_sideviewFT(30) }

spriteset(spriteset_UrsusC330_graphic, "src/gfx/other/UrsusC330.png") { template_spriteFT(8) }



switch (FEAT_ROADVEHS, SELF, switch_UrsusC330_text, param_load_multiplier) {    
	1 : return string(STR_FARM_TRACTOR_DESC, trailerD47a_maxloadmass * 3 / 2, trailerD47a_lvst * 3, string(STR_URSUSC330_DESC), UrsusC330_edisp);   
	2 : return string(STR_FARM_TRACTOR_DESC, trailerD47a_maxloadmass * 3, trailerD47a_lvst * 6, string(STR_URSUSC330_DESC), UrsusC330_edisp);    
	3 : return string(STR_FARM_TRACTOR_DESC, trailerD47a_maxloadmass * 9 / 2, trailerD47a_lvst * 9, string(STR_URSUSC330_DESC), UrsusC330_edisp);    
  return CB_FAILED;   
}   
 


switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_cap0, position_in_consist) {
	0..3: return 0;
	return CB_FAILED;
}
switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_cap1, position_in_consist) {
	0: return 0;
	1: return maxload_trailerD47a_switch;
	2..3: return 0;
	return CB_FAILED;
}
switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_cap2, position_in_consist) {
	0: return 0;
	1..2: return maxload_trailerD47a_switch;
	3: return 0;
	return CB_FAILED;
}
switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_cap3, position_in_consist) {
	0: return 0;
	1..3: return maxload_trailerD47a_switch;
	return CB_FAILED;
}

switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_capacity, cargo_subtype) {
	0: return switch_UrsusC330_cap0;
	1: return switch_UrsusC330_cap1;
	2: return switch_UrsusC330_cap2;
	3: return switch_UrsusC330_cap3;
	return CB_FAILED;
}


switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_running_cost, cargo_subtype) { 
return min(STAFF_RUNNING_COST + max(0,((UrsusC330_mass + cargo_subtype * 
                                  (trailerD47a_mass + trailerD47a_maxloadmass)) * 
(UrsusC330_edisp/10 + 4 * (UrsusC330_vmax) * (UrsusC330_vmax)) * DIESEL_ENGINE) >> 15), 255); 
} 


switch (FEAT_ROADVEHS, SELF, consist_UrsusC330_articulated_graphic_switch, position_in_consist) { 
    0 : return spriteset_UrsusC330_graphic; 
    1 : return spriteset_check_trailerD47a_switch1; 
    2 : return spriteset_check_trailerD47a_switch2; 
    3 : return spriteset_check_trailerD47a_switch3;
} 

switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_check_length, cargo_subtype) {
	0: return 1;
	1..3: return 6;
	return CB_FAILED;
}

switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_check_length2, cargo_subtype) {
	0..1: return 1;
	2..3: return 6;
	return CB_FAILED;
}

switch (FEAT_ROADVEHS, SELF, switch_UrsusC330_length, position_in_consist) { 
    0 : return 4; 
    1 : return switch_UrsusC330_check_length; 
    2 : return switch_UrsusC330_check_length2; 
return 1;
} 

switch (FEAT_ROADVEHS, SELF, UrsusC330_articulated_switch, extra_callback_info1) {
    1..3: return UrsusC330;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch(FEAT_ROADVEHS, SELF, switch_UrsusC330_weight, cargo_subtype) {
return (UrsusC330_mass + cargo_subtype * trailerD47a_mass) *4;
}

if (param_allow_slow_vehs) {
 item(FEAT_ROADVEHS, UrsusC330) { 
	property { 
	name:			   	string(STR_URSUSC330_NAME); 
	climates_available:	 	POLROAD_CLIMATES; 
	introduction_date:		date(1961, 8, 1);	    
	model_life:     (param_keep_old_vehs == 1) ? VEHICLE_NEVER_EXPIRES : ((param_keep_old_vehs == 2) ? 26+8+20 : 26+8); 
	vehicle_life:		   	20; 
	retire_early:			8; 
	reliability_decay:	    	7; 
	loading_speed:		  	5; 
	cost_factor:		    	24;          
	running_cost_factor:	min(STAFF_RUNNING_COST + ((UrsusC330_mass + 3 * trailerD47a_mass + 3 * trailerD47a_maxloadmass) * (UrsusC330_edisp/10 + 4 * (UrsusC330_vmax) * (UrsusC330_vmax)) * DIESEL_ENGINE) >> 15, 255); 
	sprite_id:		      	SPRITE_ID_NEW_ROADVEH; 
	speed:			  	UrsusC330_vmax km/h; 
	refittable_cargo_classes:      	bitmask(CC_BULK, CC_PASSENGERS, CC_OVERSIZED); 
	cargo_disallow_refit:		[TOUR]; 
	cargo_allow_refit:		[LVST, WOOD];
	misc_flags:		   	bitmask(ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_2CC); 
	refit_cost:		   	trailerD47a_refitcost; 
	cargo_age_period:	 	350; 
	power:			  	31 kW; 
	running_cost_base:	   	RUNNING_COST_ROADVEH; 
	cargo_capacity:		 	trailerD47a_maxloadmass; 
	default_cargo_type:		GRAI; 
	weight:			 	UrsusC330_mass + 3 * trailerD47a_mass;
	tractive_effort_coefficient:    0.9; 
	air_drag_coefficient:	   	0.8;  
	visual_effect:      	  	visual_effect(VISUAL_EFFECT_DIESEL, 4); 
//	road_type:			OFFR;
//sound("src/sfx/start_cmusounddesign.wav"); 
	length:      	    		4; 
    } 

    graphics { 
	default:	    	consist_UrsusC330_articulated_graphic_switch;  
	articulated_part:	UrsusC330_articulated_switch;
	purchase:	   	spriteset_UrsusC330_purchase;   
	running_cost_factor:	switch_UrsusC330_running_cost;
	purchase_running_cost_factor: min(STAFF_RUNNING_COST + ((UrsusC330_mass + 3 * trailerD47a_mass + 3 * trailerD47a_maxloadmass) * (UrsusC330_edisp/10 + 4 * (UrsusC330_vmax) * (UrsusC330_vmax)) * DIESEL_ENGINE) >> 15, 255); 
	cargo_subtype_text: 	switch_trailers3_subtype_text;
	refit_cost:		switch_trailerD47a_refit_cost;
	cargo_capacity:		switch_UrsusC330_capacity; 
	length:			switch_UrsusC330_length;
	weight:			switch_UrsusC330_weight;
	additional_text:        switch_UrsusC330_text; 
	colour_mapping:         switch_bulk_recolour;     
	visual_effect:		switch_farm_tractor_unit_effect;
	cargo_age_period:	low_passenger_comfort;
	sound_effect: return CB_RESULT_NO_SOUND;
    }  
 }
}


if ((param_allow_slow_vehs) && OFFR_EXISTS)  {
 item(FEAT_ROADVEHS, UrsusC330) { 
	property { 
	road_type:			OFFR;
    } 
 }
}


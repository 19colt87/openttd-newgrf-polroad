// part of Polish Road Set
// Graphic by McZapkie, Sojita
// NML code by McZapkie

// Scania_R730 wood logs specialized truck:


/* */
#define Scania_R730_maxloadmass 30
#define Scania_R730_mass 20
#define Scania_R730_vmax 87
#define Scania_R730displacement 15600
#define Scania_R730displacement2 16400
#define SCANIA_R730_REFIT_COST 28
spriteset(spriteset_Scania_R730_purchase, "src/gfx/truck/Scania_R730.png") { template_sprite_window_sideviewWLT(200) }

spriteset(cab_crane_loading_spriteset_Scania_R730_graphic, "src/gfx/truck/Scania_R730.png") { template_sprite8(38) }
spriteset(cab_crane_folding_spriteset_Scania_R730_graphic, "src/gfx/truck/Scania_R730.png") { template_sprite8(8) }
spriteset(cab_crane_loading3_spriteset_Scania_R730_graphic, "src/gfx/truck/Scania_R730.png") { template_sprite8(98) }
spriteset(cab_crane_wood3_spriteset_Scania_R730_graphic, "src/gfx/truck/Scania_R730.png") { template_sprite8(68) }
spriteset(cab_crane_wood2_spriteset_Scania_R730_graphic, "src/gfx/truck/Scania_R730.png") { template_sprite8(128) }
spriteset(cab_crane_wood_spriteset_Scania_R730_graphic, "src/gfx/truck/Scania_R730.png") { template_sprite8(158) }

spriteset(cab_crane_wdpr_spriteset_Scania_R730_graphic, "src/gfx/truck/Scania_R730.png") { template_sprite8(386) }

spriteset(empty_trailer_spriteset_Scania_R730_graphic, "src/gfx/truck/trailer22wood.png") { template_sprite7(8) }

spriteset(wood3_trailer_spriteset_Scania_R730_graphic, "src/gfx/truck/trailer22wood.png") { template_sprite7(38) }
spriteset(wood2_trailer_spriteset_Scania_R730_graphic, "src/gfx/truck/trailer22wood.png") { template_sprite7(68) }
spriteset(wood_trailer_spriteset_Scania_R730_graphic, "src/gfx/truck/trailer22wood.png") { template_sprite7(98) }

spriteset(wdpr_trailer_spriteset_Scania_R730_graphic, "src/gfx/truck/trailer22wood.png") { template_sprite7(148) }

spritegroup cab_spritegroup_Scania_R730_graphic {
    loaded:     [cab_crane_folding_spriteset_Scania_R730_graphic, cab_crane_wood3_spriteset_Scania_R730_graphic, cab_crane_wood2_spriteset_Scania_R730_graphic, cab_crane_wood_spriteset_Scania_R730_graphic];
    loading:    [cab_crane_loading_spriteset_Scania_R730_graphic, cab_crane_loading_spriteset_Scania_R730_graphic, cab_crane_loading3_spriteset_Scania_R730_graphic, cab_crane_wood2_spriteset_Scania_R730_graphic, cab_crane_wood_spriteset_Scania_R730_graphic];
}

spritegroup wdpr_cab_spritegroup_Scania_R730_graphic {
    loaded:     [cab_crane_folding_spriteset_Scania_R730_graphic, cab_crane_wdpr_spriteset_Scania_R730_graphic, cab_crane_wdpr_spriteset_Scania_R730_graphic, cab_crane_wdpr_spriteset_Scania_R730_graphic];
    loading:    [cab_crane_loading_spriteset_Scania_R730_graphic, cab_crane_loading_spriteset_Scania_R730_graphic, cab_crane_wdpr_spriteset_Scania_R730_graphic];
}

spritegroup trailer_spritegroup_Scania_R730_graphic {
    loaded:     [empty_trailer_spriteset_Scania_R730_graphic, wood3_trailer_spriteset_Scania_R730_graphic, wood2_trailer_spriteset_Scania_R730_graphic, wood_trailer_spriteset_Scania_R730_graphic];
    loading:    [empty_trailer_spriteset_Scania_R730_graphic, wood3_trailer_spriteset_Scania_R730_graphic, wood3_trailer_spriteset_Scania_R730_graphic, wood2_trailer_spriteset_Scania_R730_graphic, wood_trailer_spriteset_Scania_R730_graphic];
}

spritegroup wdpr_trailer_spritegroup_Scania_R730_graphic {
    loaded:     [empty_trailer_spriteset_Scania_R730_graphic, empty_trailer_spriteset_Scania_R730_graphic, wdpr_trailer_spriteset_Scania_R730_graphic];
    loading:    [empty_trailer_spriteset_Scania_R730_graphic, empty_trailer_spriteset_Scania_R730_graphic, empty_trailer_spriteset_Scania_R730_graphic, empty_trailer_spriteset_Scania_R730_graphic, wdpr_trailer_spriteset_Scania_R730_graphic];
}

switch (FEAT_ROADVEHS, SELF, trailer_Scania_R730_graphic_switch, cargo_type_in_veh) { 
    WDPR : return wdpr_trailer_spritegroup_Scania_R730_graphic; 
    return trailer_spritegroup_Scania_R730_graphic; 
} 

switch (FEAT_ROADVEHS, SELF, cab_Scania_R730_graphic_switch, cargo_type_in_veh) { 
    WDPR : return wdpr_cab_spritegroup_Scania_R730_graphic; 
    return cab_spritegroup_Scania_R730_graphic; 
} 

switch (FEAT_ROADVEHS, SELF, consist_Scania_R730_articulated_graphic_switch, position_in_consist) { 
    0 : return cab_Scania_R730_graphic_switch; 
    1 : return trailer_Scania_R730_graphic_switch; 
} 
switch (FEAT_ROADVEHS, SELF, switch_Scania_R730_articulated, extra_callback_info1) { 
    1 : return Scania_R730; 
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; 
} 
switch(FEAT_ROADVEHS, SELF, check_Scania_R730_length_switch, position_in_consist) {  
	0 : return 8; 
	1 : return 7;  
}  
switch(FEAT_ROADVEHS, SELF, check_Scania_R730_power_switch, cargo_subtype) {  
	0 : return 50; //368kW
	1 : return 74; //544kW
	return CB_FAILED;  
} 
switch(FEAT_ROADVEHS, SELF, switch_Scania_R730_running_cost, cargo_subtype) {
	0: return min(STAFF_RUNNING_COST + ((Scania_R730_mass + (Scania_R730_maxloadmass * param_load_multiplier)) * ((Scania_R730displacement)/12 + (current_speed) * (current_speed) * 14) * DIESEL_ENGINE) >> 15, 240);
	1: return min(STAFF_RUNNING_COST + ((Scania_R730_mass + (Scania_R730_maxloadmass * param_load_multiplier)) * (Scania_R730displacement2/12 + (current_speed) * (current_speed) * 14) * DIESEL_ENGINE) >> 15, 255);
	return CB_FAILED;
}
switch(FEAT_ROADVEHS, SELF, switch_Scania_R730_refit_cost, var[0x10,8,0xFF] - cargo_subtype) {
	0:				0;
	return SCANIA_R730_REFIT_COST;
}


if (param_allow_truck) {
 item(FEAT_ROADVEHS, Scania_R730) { 
	property { 
	name:			   	string(STR_SCANIA_R730_NAME); 
	climates_available:	 	POLROAD_CLIMATES; 
	introduction_date:		date(2005, 1, 1);	    
	model_life:     		VEHICLE_NEVER_EXPIRES; 
	vehicle_life:		   	20; 
	retire_early:			0; 
	reliability_decay:	    	2; 
	loading_speed:		  	12; 
	cost_factor:		    	114;      
	running_cost_factor:	min(STAFF_RUNNING_COST + ((Scania_R730_mass + (Scania_R730_maxloadmass * param_load_multiplier)) * ((Scania_R730displacement)/12 + (Scania_R730_vmax) * (Scania_R730_vmax)) * DIESEL_ENGINE) >> 15, 255); 
	sprite_id:		      	SPRITE_ID_NEW_ROADVEH; 
	speed:			  	Scania_R730_vmax km/h;  
	cargo_allow_refit:		[WOOD, WDPR]; 
	misc_flags:		   	bitmask(ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_2CC); 
	refit_cost:		   	SCANIA_R730_REFIT_COST; 
	cargo_age_period:	 	STANDARD_COMFORT; 
	power:			  	544 kW; 
	running_cost_base:	   	RUNNING_COST_ROADVEH; 
	cargo_capacity:		 	Scania_R730_maxloadmass * param_load_multiplier / 2; 
	default_cargo_type:		WOOD; 
	weight:			 	Scania_R730_mass; 
	tractive_effort_coefficient:    1; 
	air_drag_coefficient:	   	0.5; 
	visual_effect:      	  	visual_effect(VISUAL_EFFECT_DIESEL, 4); 
	sound_effect: SOUND_TRUCK_START_2; 
	length:      	    		8; 
    } 
    graphics { 
	default:	    	consist_Scania_R730_articulated_graphic_switch;  
	purchase:	   	spriteset_Scania_R730_purchase;  
	articulated_part:	switch_Scania_R730_articulated; 
	length:			check_Scania_R730_length_switch;
	power:			check_Scania_R730_power_switch;
	running_cost_factor:	switch_Scania_R730_running_cost;
	purchase_running_cost_factor: min(STAFF_RUNNING_COST + ((Scania_R730_mass + (Scania_R730_maxloadmass * param_load_multiplier)) * ((Scania_R730displacement)/12 + (Scania_R730_vmax) * (Scania_R730_vmax)) * DIESEL_ENGINE) >> 15, 255); 
	refit_cost:		switch_Scania_R730_refit_cost;
 	cargo_subtype_text: 	switch_horsepower_subtype_text;
	additional_text:        string(STR_WLT_DESC, string(STR_SCANIA_R730_DESC), Scania_R730displacement, Scania_R730displacement2, string(STR_VARIABLE_COSTS));
	visual_effect:		switch_semi_trailer_tractor_unit_effect;
	colour_mapping:         switch_livery_subtype_mapping;      
    }  
 }
}

// part of Polish Road Set
// Graphic by McZapkie, Sojita
// NML code by McZapkie

// Scania_S4 wood logs specialized truck:


/* */
#define Scania_S4_maxloadmass 20
#define Scania_S4_mass 18
#define Scania_S4_vmax 85
#define Scania_S4displacement 11700
#define Scania_S4displacement2 14200
#define SCANIA_S4_REFIT_COST 20

spriteset(spriteset_Scania_S4_purchase, "src/gfx/truck/Scania_S4.png") { template_sprite_window_sideviewWLT(200) }

spriteset(cab_crane_loading_spriteset_Scania_S4_graphic, "src/gfx/truck/Scania_S4.png") { template_sprite8(38) }
spriteset(cab_crane_folding_spriteset_Scania_S4_graphic, "src/gfx/truck/Scania_S4.png") { template_sprite8(8) }
spriteset(cab_crane_loading3_spriteset_Scania_S4_graphic, "src/gfx/truck/Scania_S4.png") { template_sprite8(98) }
spriteset(cab_crane_wood3_spriteset_Scania_S4_graphic, "src/gfx/truck/Scania_S4.png") { template_sprite8(68) }
spriteset(cab_crane_wood2_spriteset_Scania_S4_graphic, "src/gfx/truck/Scania_S4.png") { template_sprite8(128) }
spriteset(cab_crane_wood_spriteset_Scania_S4_graphic, "src/gfx/truck/Scania_S4.png") { template_sprite8(158) }
spriteset(cab_crane_wdpr_spriteset_Scania_S4_graphic, "src/gfx/truck/Scania_S4.png") { template_sprite8(389) }

spriteset(empty_trailer_spriteset_Scania_S4_graphic, "src/gfx/truck/trailer11wood.png") { template_sprite7(8) }

spriteset(wood3_trailer_spriteset_Scania_S4_graphic, "src/gfx/truck/trailer11wood.png") { template_sprite7(38) }
spriteset(wood2_trailer_spriteset_Scania_S4_graphic, "src/gfx/truck/trailer11wood.png") { template_sprite7(68) }
spriteset(wood_trailer_spriteset_Scania_S4_graphic, "src/gfx/truck/trailer11wood.png") { template_sprite7(98) }

spriteset(wdpr_trailer_spriteset_Scania_S4_graphic, "src/gfx/truck/trailer11wood.png") { template_sprite7(172) }

// spriteset(woodready_trailer_spriteset_Scania_S4_graphic, "src/gfx/truck/trailer11wood.png") { template_sprite7(128) }

spritegroup cab_spritegroup_Scania_S4_graphic {
    loaded:     [cab_crane_folding_spriteset_Scania_S4_graphic, cab_crane_wood3_spriteset_Scania_S4_graphic, cab_crane_wood2_spriteset_Scania_S4_graphic, cab_crane_wood_spriteset_Scania_S4_graphic];
    loading:    [cab_crane_loading_spriteset_Scania_S4_graphic, cab_crane_loading_spriteset_Scania_S4_graphic, cab_crane_loading3_spriteset_Scania_S4_graphic, cab_crane_wood2_spriteset_Scania_S4_graphic, cab_crane_wood_spriteset_Scania_S4_graphic];
}

spritegroup wdpr_cab_spritegroup_Scania_S4_graphic {
    loaded:     [cab_crane_folding_spriteset_Scania_S4_graphic, cab_crane_wdpr_spriteset_Scania_S4_graphic, cab_crane_wdpr_spriteset_Scania_S4_graphic, cab_crane_wdpr_spriteset_Scania_S4_graphic];
    loading:    [cab_crane_loading_spriteset_Scania_S4_graphic, cab_crane_wdpr_spriteset_Scania_S4_graphic];
}

spritegroup trailer_spritegroup_Scania_S4_graphic {
    loaded:     [empty_trailer_spriteset_Scania_S4_graphic, wood3_trailer_spriteset_Scania_S4_graphic, wood2_trailer_spriteset_Scania_S4_graphic, wood_trailer_spriteset_Scania_S4_graphic];
    loading:    [empty_trailer_spriteset_Scania_S4_graphic, wood3_trailer_spriteset_Scania_S4_graphic, wood3_trailer_spriteset_Scania_S4_graphic, wood2_trailer_spriteset_Scania_S4_graphic, wood_trailer_spriteset_Scania_S4_graphic];
}

spritegroup wdpr_trailer_spritegroup_Scania_S4_graphic {
    loaded:     [empty_trailer_spriteset_Scania_S4_graphic, empty_trailer_spriteset_Scania_S4_graphic, wdpr_trailer_spriteset_Scania_S4_graphic];
    loading:    [empty_trailer_spriteset_Scania_S4_graphic, empty_trailer_spriteset_Scania_S4_graphic, empty_trailer_spriteset_Scania_S4_graphic, wdpr_trailer_spriteset_Scania_S4_graphic];
}

switch (FEAT_ROADVEHS, SELF, trailer_Scania_S4_graphic_switch, cargo_type_in_veh) { 
    WDPR : return wdpr_trailer_spritegroup_Scania_S4_graphic; 
    return trailer_spritegroup_Scania_S4_graphic; 
} 

switch (FEAT_ROADVEHS, SELF, cab_Scania_S4_graphic_switch, cargo_type_in_veh) { 
    WDPR : return wdpr_cab_spritegroup_Scania_S4_graphic; 
    return cab_spritegroup_Scania_S4_graphic; 
} 

switch (FEAT_ROADVEHS, SELF, consist_Scania_S4_articulated_graphic_switch, position_in_consist) { 
    0 : return cab_Scania_S4_graphic_switch; 
    1 : return trailer_Scania_S4_graphic_switch; 
} 
switch (FEAT_ROADVEHS, SELF, switch_Scania_S4_articulated, extra_callback_info1) { 
    1 : return Scania_S4; 
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; 
} 
switch(FEAT_ROADVEHS, SELF, check_Scania_S4_length_switch, position_in_consist) {  
	0 : return 8; 
	1 : return 7;  
}  

switch(FEAT_ROADVEHS, SELF, check_Scania_S4_power_switch, cargo_subtype) {  
	0 : return 48; //356kW
	1 : return 54; //395kW
	return CB_FAILED;  
} 
switch(FEAT_ROADVEHS, SELF, switch_Scania_S4_running_cost, cargo_subtype) {
	0: return min(STAFF_RUNNING_COST + ((Scania_S4_mass + (Scania_S4_maxloadmass * param_load_multiplier)) * (Scania_S4displacement/10 + (current_speed) * (current_speed) * 14) * DIESEL_ENGINE) >> 15, 255);
	1: return min(STAFF_RUNNING_COST + ((Scania_S4_mass + (Scania_S4_maxloadmass * param_load_multiplier)) * (Scania_S4displacement2/10 + (current_speed) * (current_speed) * 14) * DIESEL_ENGINE) >> 15, 255);
	return CB_FAILED;
}
switch(FEAT_ROADVEHS, SELF, switch_Scania_S4_refit_cost, var[0x10,8,0xFF] - cargo_subtype) {
	0:				0;
	return SCANIA_S4_REFIT_COST;
}


if (param_allow_truck) {
 item(FEAT_ROADVEHS, Scania_S4) { 
	property { 
	name:			   	string(STR_SCANIA_S4_NAME); 
	climates_available:	 	POLROAD_CLIMATES; 
	introduction_date:		date(1995, 1, 1);	    
	model_life:     (param_keep_old_vehs == 1) ? VEHICLE_NEVER_EXPIRES : ((param_keep_old_vehs == 2) ? 17+10+19 : 17+10); 
	vehicle_life:		   	19; 
	retire_early:			10; 
	reliability_decay:	    	3; 
	loading_speed:		  	10; 
	cost_factor:		    	104;      
	running_cost_factor:	min(STAFF_RUNNING_COST + ((Scania_S4_mass + (Scania_S4_maxloadmass * param_load_multiplier)) * (Scania_S4displacement/10 + (Scania_S4_vmax) * (Scania_S4_vmax)) * DIESEL_ENGINE) >> 15, 255); 
	sprite_id:		      	SPRITE_ID_NEW_ROADVEH; 
	speed:			  	Scania_S4_vmax km/h;  
	cargo_allow_refit:		[WOOD, WDPR]; 
	misc_flags:		   	bitmask(ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_2CC); 
	refit_cost:		   	SCANIA_S4_REFIT_COST; 
	cargo_age_period:	 	STANDARD_COMFORT; 
	power:			  	395 kW; 
	running_cost_base:	   	RUNNING_COST_ROADVEH; 
	cargo_capacity:		 	Scania_S4_maxloadmass * param_load_multiplier / 2; 
	default_cargo_type:		WOOD; 
	weight:			 	Scania_S4_mass; 
	tractive_effort_coefficient:    1; 
	air_drag_coefficient:	   	0.5; 
	visual_effect:      	  	visual_effect(VISUAL_EFFECT_DIESEL, 4); 
	sound_effect: SOUND_TRUCK_START_2; 
	length:      	    		8; 
    } 
    graphics { 
	default:	    	consist_Scania_S4_articulated_graphic_switch;  
	purchase:	   	spriteset_Scania_S4_purchase;  
	articulated_part:	switch_Scania_S4_articulated; 
	length:			check_Scania_S4_length_switch;
	power:			check_Scania_S4_power_switch;
	running_cost_factor:	switch_Scania_S4_running_cost;
	purchase_running_cost_factor: min(STAFF_RUNNING_COST + ((Scania_S4_mass + (Scania_S4_maxloadmass * param_load_multiplier)) * (Scania_S4displacement/10 + (Scania_S4_vmax) * (Scania_S4_vmax)) * DIESEL_ENGINE) >> 15, 255); 
	refit_cost:		switch_Scania_S4_refit_cost;
 	cargo_subtype_text: 	switch_horsepower_subtype_text;
	additional_text:        string(STR_WLT_DESC, string(STR_SCANIA_S4_DESC), Scania_S4displacement, Scania_S4displacement2, string(STR_VARIABLE_COSTS));
	visual_effect:		switch_semi_trailer_tractor_unit_effect;
	colour_mapping:         switch_livery_subtype_mapping;      
    }  
 }
}
